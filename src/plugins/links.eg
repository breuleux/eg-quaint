
require:
   ..tools ->
      shed
   ..ast ->
      Text

provide:
   install


make-link{engine, match type, label, url, prefix = ""} =
   .link ->
      url2 = span % {prefix, engine.litgen{url}}
      a %
         href = url2
         if{label == Text{""}, url2, engine.gen{label}}
   .image ->
      lbl = engine.litgen{label}
      img %
         src = span % {prefix, engine.litgen{url}}
         alt = lbl
         title = lbl
   .audio ->
      audio %
         controls = true
         source % src = span % {prefix, engine.litgen{url}}
         "No support for audio."
   .video ->
      video %
         controls = true
         source % src = span % {prefix, engine.litgen{url}}
         "No support for audio."
      


install{@} =

   sr{engine} =
      match engine.documents.meta.get-raw{.site-rel}:
         "." -> ""
         s -> '{s}/'

   @register-rules with {
      "\\maybe\\label @@ \\url"{engine, {=> label, url => shed! url}} =
         make-link{engine, .link, label, url}

      "\\maybe\\label @@ image : \\url"{engine, {=> label, url => shed! url}} =
         make-link{engine, .image, label, url}

      "@@ audio : \\url"{engine, {url => shed! url}} =
         make-link{engine, .audio, "", url}

      "@@ video : \\url"{engine, {url => shed! url}} =
         make-link{engine, .video, "", url}


      "\\maybe\\label @@@ \\url"{engine, {=> label, url => shed! url}} =
         make-link{engine, .link, label, url, sr{engine}}

      "\\maybe\\label @@@ image : \\url"{engine, {=> label, url => shed! url}} =
         make-link{engine, .image, label, url, sr{engine}}

      "@@@ audio : \\url"{engine, {url => shed! url}} =
         make-link{engine, .audio, "", url, sr{engine}}

      "@@@ video : \\url"{engine, {url => shed! url}} =
         make-link{engine, .video, "", url, sr{engine}}
   }

