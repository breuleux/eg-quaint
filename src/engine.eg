

require:
   opg ->
      Source
   .lang ->
      parse
   .ast ->
      QAst, Text, Oper, Seq
      Into, Deferred
   .etools
   .tools as tools ->
      makeExtractor
      normalize
      may-fail
   .doc ->
      SeqDocument
   .util ->
      insertSorted
      transform
   "./html" ->
      HTMLFormatter
   /html
   /dom
   fs
   hyper-replace

provide:
   Dispatcher
   Engine
   Spec
   processDocuments

class Dispatcher:

   constructor{} =
      @dispatch = {}

   gen handlers{node} =
      key = node.operator or ""
      if not key:
         console.error with
            "Node lacks an operator key. That may be indicative of a bug."
      match @dispatch[key]:
         undefined? ->
            false
         candidates ->
            candidates each c ->
               match node:
                  c! parts ->
                     yield {parts, c}
                  else ->
                     pass
            false

   register{*match} =
      {Spec? handler} ->
         key = handler.operator
         l = [@dispatch[key] or= {}]
         insertSorted{l, handler} with
            {{specificity => match s1}, {specificity => s2}} ->
               ;; Do not return 0 because it will cause a replacement
               > s2 -> -1
               else -> 1
      {rule, fn} ->
         @register{Spec{rule, fn}}

   clone{} =
      d = Dispatcher{}
      d.dispatch = object with items{@dispatch} each {key, l} ->
         {key, l.slice{0}}
      d


class Engine:

   constructor{Array! plugins} =
      @next = tools.next
      @top = true
      @source-name = null
      @is-quaint-engine = true
      @components = {
         escape{tags, props, {child}} =
            child
         group{tags, props, match} =
            {child} -> child
            children -> children
      }
      @dispatch = Dispatcher{}
      @documents = {=}
      @formatters = {
         html = HTMLFormatter{@}
      }
      @regexps = {
         start = {}
         add = {=}
         rm = {=}
      }
      @methods = {}
      @macros = {=}
      @resolvers = {=}
      @resources = {=}
      @plug{*plugins}
      @tools = tools
      @etools = etools
      @template = null
      @deferred = Deferred
      @into = Into

   fork-plug{*plugins} =
      @fork{}.plug{*plugins}

   plug{*plugins} =
      plugins each match plugin ->
         Function? f -> f{@}
         {=> install-to} ->
            plugin.install-to{@}
      @


   .gen{match node} =
      Text? {t} or Oper? {t} ->
         l = node.location
         r = R.g"(~|\\[\\~!@#$%^&*_\-+=<>/?;:.`|(){}\[\],.])"
         res = enumerate{node.location.text{}.split{r}} each {i, match part} ->
            when (i mod 2) === 0 ->
               part
            "~" -> ""
            s ->
               escape % s[1]
         match res:
            {x} -> x
            else -> res

      Seq? {*parts} ->
         result = f{} where f{} =
            @dispatch.handlers{node} each {values, handler} ->
               extra = {_node = node
                        _wide = node.width == .wide
                        _op = node[1]}
               x = may-fail{-> handler.gen{@, extra & values}}
               match x:
                  == @next -> pass
                  undefined? -> return ""
                  else -> return x
            % [parts each part -> @gen{part}]
         result

      null? or undefined? ->
         ""

      x and [ENode? or Array? or String? or Boolean?] ->
         x

      Error? e ->
         if e.id as id:
            div.error %
               id = 'ERR{id}'
               '[#{id}] '
               e.stack
         else:
            div.error % e.stack

   gen-from-source{match src} =
      String? text ->
         @gen-from-source{Source{text, "<quaint>"}}
      Source? src ->
         parsed = parse{src}
         ;; TODO: we should use soft-fork, but that works poorly with
         ;; some plugins that change engine.evaluator, because that will
         ;; only change the soft fork...
         ;; @soft-fork{source-name = src.url}.gen{parsed}
         old-source-name = @source-name
         @source-name = src.url
         result = @gen{parsed}
         @source-name = old-source-name
         result
      node ->
         @gen{node}

   run{src} =
      generated = @gen-from-source{src}
      docs = @documents
      main = processDocuments{generated, docs}
      docs &: {= main}

   translate{src, format, options = {=}} =

      finalize{node, _regexps} =
         var add = {}
         var rm = {}
         [node.tags or {}] each tag ->
            add ++= @regexps.add[tag] or {}
            rm ++= @regexps.rm[tag] or {}
         regexps =
            if add.length or rm.length:
               then:
                  r = new Set{_regexps}
                  add each entry -> r.add{entry}
                  rm each entry -> r.delete{entry}
                  r
               else:
                  _regexps

         target = match node:
            String? s -> s
            Array? a -> a
            ENode? e -> e.children
            else -> {node}
         regexps-arg = regexps each {r, repl} ->
            {pattern = r, replacement = repl}
         new-children = hyper-replace{target, regexps-arg}
         match node:
            ENode? e ->
               ;; e2 = ENode{e.tags, e.props, Array! new-children}
               e.children = if{new-children === '', {}, Array! new-children}
               collapse{e, regexps}
            when Array? new-children ->
               new-children each
                  String? s -> s
                  other -> collapse{other, regexps}
            else ->
               new-children

      collapse{node, regexps} =
         etools.merge-children{node, false} with sub-node ->
            finalize{sub-node, regexps}

      results = @run{src}

      main = normalize{collapse{top, new Set{@regexps.start}}} where top =
         if options.paragraph:
            p % results.main
         else:
            top % results.main

      {result, files} = match format:
         .enode ->
            {main, {=}}
         .text ->
            {main.toString{}, {=}}
         .dom ->
            {dom{main}, {=}}
         when @formatters[format] ->
            @formatters[format].format{main, @resources, options.resources}
         else ->
            throw E.cli-error.unknown-format{'Cannot generate code in format: "{format}"'}

      if @template and not options.no-template:
         tsrc =
            match @read-resource{.template, @template}:
               {=> file, => contents} -> Source{contents, file}
               {=> contents} or String? contents -> Source{contents, "<quaint>"}
         @template = null
         @setenv{.body, raw % result}
         {contents => r2, files => f2} = @translate{tsrc, format, options}
         {contents = r2, files = files & f2}
      else:
         {contents = result, files = files}

   toHTML{src, options = {=}} =
      @translate{src, .html, options}.contents

   toENode{src, options = {=}} =
      @translate{src, .enode, options}.contents


   write{path, src, type, options = {=}} =
      w = options.write or fs.write-file-sync
      {=> contents, => files} = @translate{src, type, options}
      w{path, contents}
      items{files} each {path, contents} ->
         w{path, contents}

   writeHTML{path, src, options = {=}} =
      @write{path, src, .html, options}


   register-components{cmps} =
      @components &: cmps

   register-document{*args} =
      console.error{'Deprecated: Engine::registerDocument, use Engine::registerDocuments'}
      @registerDocuments.apply{@, args}

   register-documents{docs} =
      items{docs} each {name, maker} ->
         @documents[name] = maker
      @

   register-methods{methods} =
      @methods ++= keys{methods}
      this &: methods
      @

   register-macros{macs} =
      @macros &: macs
      @

   register-postprocessor{*match} =
      {expr, repl, options = {=}} ->

         options as {
            whitelist => wl = null
            blacklist => bl = {.raw, .pre, .code, .script, .style, .kbd, .escape}
         }

         entry = {if{String? expr, RegExp{expr, "g"}, expr}, repl}

         if wl:
            wl each tag ->
               [@regexps.add[tag] or= {}].push{entry}
         else:
            @regexps.start.push{entry}

         bl each tag ->
            [@regexps.rm[tag] or= {}].push{entry}

         @
      {each {expr, repl, options = {=}}} ->
         @register-postprocessor{expr, repl, options}
         @

   register-resolvers{resolvers} =
      items{resolvers} each {name, res} ->
         @resolvers[name] or= {
            fns = {}
            keys = {=}
         }
         match res:
            Function? -> @resolvers[name].fns.unshift{res}
            else      -> @resolvers[name].keys &: res
         ;; @resolvers[name] or= {}
         ;; @resolvers[name].push{res}
      @

   register-resources{resources} =
      items{resources} each {name, data} ->
         if data.path and not data.path-is-url and not data.full-path:
            {=> path, name => new-name = null} = @read-resource{.resource, data.path, {read = false}}
            data.full-path = path
            data.name = new-name or name

         ;; if data.path and data.contents === true:
         ;;    data.contents = @read-resource-contents{.resource, data.path}

         ;; if @resources[name] as prev:
         ;;    if prev.contents !== data.contents:
         ;;       throw E.quaint.resource with
         ;;          'Duplicate resource "{name}" has inconsistent content.'

         @resources[data.name] = data

   register-rule{*args} =
      console.error{'Deprecated: Engine::registerRule, use Engine::registerRules'}
      @register-rules.apply{@, args}

   register-rules{*match} =
      {rule, handler} ->
         @dispatch.register{rule, handler}
         @
      {rules} ->
         items{rules} each {name, match} ->
            Function? handler ->
               @dispatch.register{name, handler}
            {rule, handler} ->
               @dispatch.register{rule, handler}
         @

   clear-rules{} =
      @dispatch = Dispatcher{}
      @

   read-resource{type, filename, var options} =
      options = {
         fallback-to-default = true
         read = true
         requested-from = @source-name
      } & options
      resolver = @resolvers[type]
      var error = null

      if not resolver:
         if options.fallback-to-default and type !== "default":
            return @read-resource{.default, filename, options}
         else:
            throw E.cannot-resolve-group{'Resources of group "{type}" cannot be resolved (error when trying to resolve "{filename}")'}

      match resolver.keys[filename] as result:
         undefined? ->
            resolver.fns each fn ->
               try:
                  return fn{filename, options}
               catch e:
                  error = e
            if error:
               throw error
            elif options.fallback-to-default:
               @read-resource{.default, filename, options}
            else:
               throw E.cannot-resolve{'Resource "{filename}" of group "{type}" could not be resolved.'}

         else ->
            result

      ;; to-try = [@resolvers[type] or {}] ++
      ;;    if{options.fallback-to-default, @resolvers.default, {}}
      ;; var error = null
      ;; to-try each resolver ->
      ;;    try:
      ;;       return resolver{filename}
      ;;    catch e:
      ;;       error = e
      ;; if error:
      ;;    throw error
      ;; else:
      ;;    throw E.cannot-resolve-type{'Resources of type "{type}" cannot be resolved.'}

   read-resource-contents{type, filename, options} =
      match @read-resource{type, filename, options}:
         {=> contents} -> contents
         contents -> contents

      ;; resolver =
      ;;    @resolvers[type] or
      ;;       if{options.fallback-to-default, @resolvers.default, null}
      ;; if not resolver:
      ;;    throw E.cannot-resolve-type{'Resources of type "{type}" cannot be resolved.'}
      ;; resolver{filename}

   read-resource-text{type, filename, options} =
      match @read-resource{type, filename, options}:
         {=> contents} -> contents
         contents -> contents

   soft-fork{changes = {=}} =
      e = Engine{{}}
      e.top = @top
      e.dispatch = @dispatch
      e.components = @components
      e.documents = @documents
      e.evaluator = @evaluator
      e.formatters = @formatters
      e.regexps = @regexps
      e.macros = @macros
      e.methods = @methods
      e.methods each m -> e[m] = this[m]
      e.resolvers = @resolvers
      e.resources = @resources
      e.template = @template
      e.actual = [@]
      e &: changes
      e

   fork{} =
      e = @soft-fork{}
      e.top = false
      e.dispatch = @dispatch.clone{}
      e.evaluator = @evaluator.fork{}
      e.formatters = clone{@formatters}
      ;; TODO: regexps should be deep cloned
      e.regexps = clone{@regexps}
      e.macros = clone{@macros}
      e.methods = clone{@methods}
      e.methods each m -> e[m] = this[m]
      e.resolvers = clone{@resolvers}
      e.actual = e
      e

   eval{text, env, loc = null} =
      @evaluator.eval{text, env, loc}

   setenv{*match} =
      {name, value} ->
         @evaluator.setenv{name, value}
         @
      {env} ->
         items{env} each {k, v} ->
            @setenv{k, v}
         @


   redefer{match node, fn} =
      Deferred? ->
         Deferred with {path, docs} ->
            res =
               try:
                  node.generate{path, docs}
               catch e:
                  e
            @redefer{res, fn}
      else ->
         fn{node}


class Spec:
   constructor{makeExtractor! {@operator, @specificity, @extractor}, @gen} =
      pass
   [^Symbol.project-no-exc]{@extractor! match} =
      false? ->
         {false, null}
      values ->
         {true, values}



processDocuments{x, documents, maxiter = 10} =

   var forges = new Set{}
   deps = new Map{}

   populate{match node, documents, replace, path} =

      [redoc %]? ->
         {child} = node.children
         populate{child, newdocs, replace, path} where newdocs =
            match node.props.documents:
               Function? f -> f{documents}
               docs -> documents & docs

      Into? ->
         docname = node.document
         match documents[docname]:
            undefined? when node.create ->
               documents[docname] = node.create{}
            undefined? when node.important ->
               throw E.no_document{docname}
            else ->
               pass

         match documents[docname]:
            false? ->
               ""
            {delay => true?} ->
               node
            doc ->
               ;; doc.register.apply{doc, {path} ++ node.children}
               doc.register.call{doc, path, node.value}
               ""

      Deferred? ->
         forges.add{{node, documents, replace, path}}
         "PLACEHOLDER"

      ENode? ->
         rval = etools.inherit{node, tags, props, children} where
            tags = node.tags
            props = object with items{node.props} each {k, match v} ->
               _ -> {k, populate{v, documents, replace, path ++ {k}}} where
                  replace{x} = rval.props[k] = x
            children =
               enumerate{node.children} each {i, c} ->
                  populate{c, documents, replace, path ++ {i}} where
                     replace{x} = rval.children[i] = x
         rval

      Array? ->
         rval =
            enumerate{node} each {i, c} ->
               populate{c, documents, replace, path ++ {i}} where
                  replace{x} = rval[i] = x
         rval

      else ->
         node

   var res = populate{% {x}, documents, replace, {}} where
      replace{x} = res = x

   var niter = 0
   while forges.size != 0 and niter < maxiter:

      {_forges, forges} = {forges, new Set{}}
      niter += 1

      _forges each forge and {node, docs, replace, path} ->

         ;; Reset accessed/modified flags for all documents
         items{docs} each {_, doc} when doc.resetFlags ->
            doc.resetFlags{}

         ;; Execute generator, process new node, perform the replacement.
         replace with populate{newnode, docs, replace, path} where
            newnode = may-fail{-> node.generate{path, docs}}

         ;; For all modified documents, every process that accessed
         ;; them is put back in the queue for reprocessing
         items{docs} each {_, doc} when doc.modified ->
            [deps.get{doc} or {}] each
               forge -> forges.add{forge}
            deps.set{doc, {}}

         ;; For all documents it accessed, the current generator is
         ;; put in the dependencies for that document. Since we do this
         ;; after checking for modifications, a generator may modify
         ;; a document it accesses without causing an infinite loop.
         items{docs} each {_, doc} when doc.accessed ->
            if not deps.has{doc}: deps.set{doc, {}}
            deps.get{doc}.push with forge

   res.children[0]

